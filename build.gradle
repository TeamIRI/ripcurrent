import java.util.regex.Pattern

plugins {
    id 'application'
    id 'distribution'
    id 'com.github.hierynomus.license' version '0.16.1'
    id "de.undercouch.download" version "4.1.1"
    id 'org.gradle.crypto.checksum' version '1.1.0'
}
apply plugin: 'java'
apply plugin: 'application'


group 'com.iri.ripcurrent'
mainClassName = 'Main'

repositories {
    mavenCentral()
}
// A map of all the required run and test system properties.
def runtimeProperties = [
        "APP_HOME": buildDir
]
jar {
    archiveBaseName = 'ripcurrent'
    archiveVersion = '1.0.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
distributions {
    main {
        distributionBaseName = 'ripcurrent'
        contents {
            into("conf") {
                from("conf") {
                    include 'config.properties'
                }
            }
            into("docs/dependencies") {
                from(tasks.downloadLicenses)
            }
        }
    }
}

// Downloads all the open source licenses that are used by dependency and lists them in
// html, json, and xml formats.
downloadLicenses {
    reportByDependency = false
    xml = false
    json = false
}

license {
    ignoreFailures = true
}
startScripts {
    applicationName = 'ripcurrent'
    defaultJvmOpts = ['-DAPP_HOME=_APP_HOME',
                      '-Dlog4j.configurationFile=_APP_HOME/conf/log4j.xml']

    doLast {
        def pattern = Pattern.compile("(?<=CLASSPATH=).+(?=\$)", Pattern.MULTILINE)
        unixScript.text = pattern.matcher(unixScript.text).replaceFirst("_APP_HOME/lib/*")
        windowsScript.text = pattern.matcher(windowsScript.text).replaceFirst("_APP_HOME\\\\lib\\\\*")

        unixScript.text = unixScript.text.replace("_APP_HOME", "'\${APP_HOME}'")
        windowsScript.text = windowsScript.text.replace("_APP_HOME", '"%APP_HOME%"')
    }
}
task copyConfig(type: Copy) {
    from "$rootDir/conf"
    into "$buildDir/conf"
}

// When running the application in dev
run {
    dependsOn(copyConfig)
    systemProperties runtimeProperties
    systemProperty "log4j.configurationFile", "$rootDir/conf/log4j.xml"
}
dependencies {
    // https://mvnrepository.com/artifact/io.debezium/debezium-embedded
    implementation group: 'io.debezium', name: 'debezium-embedded', version: debeziumVersion
// https://mvnrepository.com/artifact/io.debezium/debezium-connector-mysql
    implementation group: 'io.debezium', name: 'debezium-connector-mysql', version: debeziumVersion
    // https://mvnrepository.com/artifact/io.debezium/debezium-connector-oracle
    implementation group: 'io.debezium', name: 'debezium-connector-oracle', version: debeziumVersion
    // https://mvnrepository.com/artifact/io.debezium/debezium-connector-postgres
    implementation group: 'io.debezium', name: 'debezium-connector-postgres', version: debeziumVersion
    // https://mvnrepository.com/artifact/io.debezium/debezium-connector-sqlserver
    implementation group: 'io.debezium', name: 'debezium-connector-sqlserver', version: debeziumVersion
// https://mvnrepository.com/artifact/io.debezium/debezium-api
    implementation group: 'io.debezium', name: 'debezium-api', version: debeziumVersion
    implementation 'com.google.code.gson:gson:2.8.9'
    // https://mvnrepository.com/artifact/com.oracle.database.jdbc/ojdbc8
    implementation group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '21.5.0.0'

// https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'

}

